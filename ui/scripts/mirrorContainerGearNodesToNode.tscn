[gd_scene load_steps=2 format=3 uid="uid://dq74ovxo7ghob"]

[sub_resource type="GDScript" id="GDScript_bcx52"]
script/source = "extends Node

var parent_container

@onready var empty_holder_child_count = $templateCellHolder.get_child_count()

@export var cell_container : Node

@export var loot_bag_scene: PackedScene


func _init():
  tree_entered.connect(on_tree_entered)
  tree_exited.connect(on_tree_exited)


func on_tree_entered():
  on_tree_exited() #exit tree and the re-attach
  var new_parent_container = Lib.get_parent_in_group(self, 'gear_container')
  if parent_container == new_parent_container:
    return #still connected
  parent_container = new_parent_container
  (func(): 
    if cell_container == null:
      push_error('No cell container export variable given')
      return
    
    #add cell holders to hboxcontainer
    var cell_count = Comp.get_prop(parent_container, 'ui_gear_cells', 6)
    for i in range(0, cell_count):
      make_new_holder_child()
    
    #add current gear ndoes:
    for possible_gear_node in parent_container.get_children():
      if not possible_gear_node.is_in_group('gear'):
        continue
      on_added_container_node(possible_gear_node)
    
    #for future gear nodes:
    parent_container.child_entered_tree.connect(on_added_container_node)
  ).call_deferred()
  

func on_tree_exited():
  if parent_container == null:
    return #already unparented
  var current_gear_container = Lib.get_parent_in_group(self, 'gear_container')
  if current_gear_container != null:
    return #not unparented from container
  parent_container.child_entered_tree.disconnect(on_added_container_node)


func make_new_holder_child():
  var cell_holder_template = $templateCellHolder
  var new_cell_holder = cell_holder_template.duplicate(DUPLICATE_GROUPS & DUPLICATE_SCRIPTS & DUPLICATE_SIGNALS)
  new_cell_holder.visible = true
  new_cell_holder.process_mode = Node.PROCESS_MODE_INHERIT
  new_cell_holder.add_to_group('ui_cell_holder')
  Comp.set_prop(new_cell_holder, 'get_parent_container', func(): return parent_container)
  var click_area = new_cell_holder.get_node('Area2D')
  Comp.set_prop(click_area, 'on_direct_mouse_event', holder_on_direct_mouse_event.bind(new_cell_holder))
  Comp.set_prop(click_area, 'own_dragging_started', holder_own_dragging_started.bind(new_cell_holder))
  Comp.set_prop(click_area, 'own_dragging_ended', holder_own_dragging_ended.bind(new_cell_holder))
  cell_container.add_child(new_cell_holder)


#A new child of the closest parent gear_container group node was added
func on_added_container_node(new_node: Node):
  if not new_node.is_in_group('gear'):
    return
  
  if not Comp.has_prop(new_node, 'make_ui_cell'):
    return
  
  #check node hasn't been added already
  for cell_holder in cell_container.get_children():
    var gear_cell_node = Lib.get_first_immediate_child_in_group(cell_holder, 'ui_gear_cell')
    if gear_cell_node == null:
      continue
    var cells_gear_node = Comp.call_method_or(gear_cell_node, 'get_gear_node', [], null)
    if cells_gear_node == new_node:
      return #new node is already present
  
  #add to first empty holder
  for cell_holder in cell_container.get_children():
    if cell_holder.get_child_count() == empty_holder_child_count: #only add to empty cell holders
      var gear_cell_node = Comp.call_method(new_node, 'make_ui_cell')
      gear_cell_node.add_to_group('ui_gear_cell')
      cell_holder.add_child(gear_cell_node)
      return


#Check whether given gear container node can fit one more node
func check_container_has_room(gear_container: Node):
  var max_gear_count = Comp.get_prop(gear_container, 'ui_gear_cells', 0)
  var gear_children = Lib.get_immediate_children_in_group(gear_container, 'gear')
  return gear_children.size() + 1 <= max_gear_count


#Call click handlers on gear node, or move gear nodes on shift-click
func holder_on_direct_mouse_event(event: InputEventMouse, holder_node: Node):
  get_viewport().set_input_as_handled()
  if not (event is InputEventMouseButton):
    return
  if event.pressed:
    return
  if Lib.is_dragging:
    return
  
  var gear_cell_node = Lib.get_first_immediate_child_in_group(holder_node, 'ui_gear_cell')
  if gear_cell_node == null:
    return #no gear cell in this holder, ie: its empty
  
  #Move gear between first open container and  if shift left-clicked
  if event.shift_pressed and (event.button_index == MOUSE_BUTTON_LEFT):
    var gear_node = Comp.call_method_or(gear_cell_node, 'get_gear_node', [], null)
    if gear_node == null:
      return #probably no get_gear_node method on cell, otherwise cell's gear node shouldnt be null (an error)
    
    #Is clicked gear node not inside the player?
    if not Lib.is_child_of(Lib.player_character, gear_node):
      #Does player have room for this item?
      var has_room = check_container_has_room(Lib.player_character)
      if not has_room:
        return #player cant fit the node
      #move gear node to character
      gear_node.reparent(Lib.player_character)
      return
    #Not a child of player, must be in a loot bag / gear container
    
    #trying moving to first open loot bag
    var loot_display = null
    for possible_open_loot_display in get_viewport().get_tree().get_nodes_in_group('loot_bag_display_popup'):
      if possible_open_loot_display.visible == true:
        loot_display = possible_open_loot_display
        break
    if loot_display == null:
      return #no open loot display found
    
    var loot_container = Lib.get_parent_in_group(loot_display, 'gear_container')
    if loot_container == null:
      push_error('Loot container popup has no container parent')
      return
    
    #Does container have room for this item?
    var has_room = check_container_has_room(loot_container)
    if not has_room:
      return #loot_container cant fit the node
    gear_node.reparent(loot_container)
    return
  #
  
  #shift not pressed, just a regular mouse button release
  match event.button_index:
    MOUSE_BUTTON_LEFT: Comp.run_method(gear_cell_node, 'handle_left_clicked', [event])
    MOUSE_BUTTON_RIGHT: Comp.run_method(gear_cell_node, 'handle_right_clicked', [event])


#Try to find the best representation of a gear_cell to show near the mouse when dragging the cell
func get_gear_cell_drag_texture(gear_cell: Node):
  var cell_drag_texture = Comp.call_method_or(gear_cell, 'get_drag_texture', [], null)
  if cell_drag_texture != null:
    return cell_drag_texture
  
  cell_drag_texture = Comp.get_prop(gear_cell, 'dragging_display_texture')
  if cell_drag_texture != null:
    return cell_drag_texture
  
  cell_drag_texture = gear_cell.texture if ('texture' in gear_cell) else null
  if cell_drag_texture != null:
    return cell_drag_texture
  
  cell_drag_texture = Lib.get_first_immediate_child_texture(gear_cell)
  if cell_drag_texture != null:
    return cell_drag_texture
  
  return null


#Set the dragging displayed texture
func holder_own_dragging_started(holder_node: Node):
  var gear_cell = Lib.get_first_immediate_child_in_group(holder_node, 'ui_gear_cell')
  if gear_cell == null:
    return
  var cell_drag_texture = get_gear_cell_drag_texture(gear_cell)
  Lib.dragging_display.texture = cell_drag_texture if (cell_drag_texture != null) else null


#At the end of a drag-drop, try to move the holder's gear node to whatever was dropped over
func holder_own_dragging_ended(_max_z_collider, holder_node: Node):
  var gear_cell = Lib.get_first_immediate_child_in_group(holder_node, 'ui_gear_cell')
  if gear_cell == null:
    return #holder_node is an empty holder
  
  var gear_node = Comp.call_method_or(gear_cell, 'get_gear_node', [], null)
  if gear_node == null:
    return #probably no get_gear_node method on gear_cell
  
  #Whats at the dragging end point?
  #query colliders at the mouse:
  var mouse_position = Lib.player_character.get_global_mouse_position()
  var ordered_collider_query = Lib.get_z_ordered_collider_query_at(mouse_position)
  
  #Is the drop over a ui_cell_holder? 
  var other_cell_holder = Lib.get_first_nonnull_return(ordered_collider_query, func(collider_result):
    var collider = collider_result.collider
    return Lib.get_parent_in_group(collider, 'ui_cell_holder')
  )
  if other_cell_holder != null:
    #swap clicked holder cell's and this cell's contents
    if Lib.is_child_of(parent_container, other_cell_holder):
      swap_sibling_holders_cells(holder_node, other_cell_holder)
    else: #is other container's cell holder
      swap_foreign_holders_cells(holder_node, other_cell_holder)
    return
  #It wasn't over a ui_cell_holder...
  
  #Was the drop over a container body (eg: a loot bag)
  var gear_container = Lib.get_first_nonnull_return(ordered_collider_query, func(collider_result):
    var collider = collider_result.collider
    return Lib.get_parent_in_group(collider, 'gear_container')
  )
  if (gear_container != null) and Comp.get_prop(gear_container, 'accepts_gear_drops', false):
    var has_room = check_container_has_room(gear_container)
    if not has_room:
      return #gear_container cant fit the node
    gear_node.reparent(gear_container)
    return
  #It wasn't over a container body

  #Otherwise, dropped over nothing, so spawn a loot bag in the world and put the gear there
  var game_world = Lib.get_parent_in_group(self, 'game_world')
  if game_world == null:
    push_error('No game_world group parent to place loot bag droo')
    return
  var new_loot_bag = (loot_bag_scene if loot_bag_scene else Game.default_loot_bag).instantiate()
  new_loot_bag.global_position = Lib.player_character.get_global_mouse_position()
  gear_node.reparent(new_loot_bag)
  game_world.add_child(new_loot_bag)
  

#Move sibling cell holders around in the same UI (for swapping gear inventory positions)
func swap_sibling_holders_cells(holder_node_1: Node, holder_node_2: Node):
  var gear_cell_1 = Lib.get_first_immediate_child_in_group(holder_node_1, 'ui_gear_cell')
  var gear_cell_2 = Lib.get_first_immediate_child_in_group(holder_node_2, 'ui_gear_cell')
  if gear_cell_1 != null:
    gear_cell_1.reparent(holder_node_2, false)
  if gear_cell_2 != null:
    gear_cell_2.reparent(holder_node_1, false)


#Move gear nodes between two gear cells that aren't in the same container's UI
#For when drag-dropped over a different container's ui cell holder
func swap_foreign_holders_cells(holder_node_1: Node, holder_node_2: Node):
  var gear_cell_1 = Lib.get_first_immediate_child_in_group(holder_node_1, 'ui_gear_cell')
  var gear_cell_2 = Lib.get_first_immediate_child_in_group(holder_node_2, 'ui_gear_cell')
  if gear_cell_1 != null:
    var parent_container_2 = Comp.call_method_or(holder_node_2, 'get_parent_container', [], null)
    var gear_node_1 = Comp.call_method_or(gear_cell_1, 'get_gear_node', [], null)
    Comp.run_method(gear_cell_1, 'retain_on_reparent_once')
    gear_cell_1.reparent(holder_node_2, false)
    gear_node_1.reparent(parent_container_2, false)
  if gear_cell_2 != null:
    var parent_container_1 = Comp.call_method_or(holder_node_1, 'get_parent_container', [], null)
    var gear_node_2 = Comp.call_method_or(gear_cell_2, 'get_gear_node', [], null)
    Comp.run_method(gear_cell_2, 'retain_on_reparent_once')
    gear_cell_2.reparent(holder_node_1, false)
    gear_node_2.reparent(parent_container_1, false)
























"

[node name="mirrorContainerGearNodesToNode" type="Node"]
editor_description = "This looks at the closest `gear_container`-group parent node and for each of that node's immediate `gear`-group child nodes it calls those childrens' `make_ui_cell` methods and collect the `Control` nodes they return and adds them as children to the `cell_container` node inside special holder nodes
In other words, this takes the player's / loot bag's (whichever this is a deep child of) gear group child nodes, gets their ui cells from them (the gear nodes create their own ui cells), and places those ui cells as children of the `cell_container` node's holder nodes

The holder nodes which gear nodes' ui cells go into are generated automatically as children of `cell_container` and are just duplicates of this node's `templateCellHolder` child. So, this node has to have a `templateCellHolder` child. Find and look at the`mirrorContainerGearNodesToNode` in `res://ui/playerUi.tscn` for an example `templateCellHolder`
Further node about holder node templates: the `templateCellHolder` child of this node must have an `Area2D` which this places its `on_direct_mouse_event`, `own_dragging_started`, and `own_dragging_ended` closures into. These are for clicking and dragging detection in the UI

Cell holders do a lot of stuff, so look at the script on this node to see more details"
script = SubResource("GDScript_bcx52")
